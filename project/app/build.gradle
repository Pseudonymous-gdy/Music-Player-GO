plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'com.google.gms.google-services'
}

android {
    compileSdkVersion 34
    //noinspection GradleDependency
    defaultConfig {
        applicationId "com.example.musicplayergo"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 187
        versionName "4.4.24"

        vectorDrawables.useSupportLibrary true
    }

    //retrieve version number
    applicationVariants.configureEach { variant ->
        resValue "string", "versionName", variant.versionName
    }

    buildFeatures {
        viewBinding true
    }

    // Signing via environment variables (used by CI)
    def SIGNING_KEYSTORE_PATH = System.getenv("SIGNING_KEYSTORE_PATH")
    def SIGNING_KEYSTORE_PASSWORD = System.getenv("SIGNING_KEYSTORE_PASSWORD")
    def SIGNING_KEY_ALIAS = System.getenv("SIGNING_KEY_ALIAS")
    def SIGNING_KEY_PASSWORD = System.getenv("SIGNING_KEY_PASSWORD")

    signingConfigs {
        release {
            if (SIGNING_KEYSTORE_PATH && SIGNING_KEYSTORE_PASSWORD && SIGNING_KEY_ALIAS && SIGNING_KEY_PASSWORD) {
                storeFile file(SIGNING_KEYSTORE_PATH)
                storePassword SIGNING_KEYSTORE_PASSWORD
                keyAlias SIGNING_KEY_ALIAS
                keyPassword SIGNING_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "@string/app_name_release"
            if (SIGNING_KEYSTORE_PATH && SIGNING_KEYSTORE_PASSWORD && SIGNING_KEY_ALIAS && SIGNING_KEY_PASSWORD) {
                signingConfig signingConfigs.release
            } else {
                println("[Gradle] Release signing not configured; producing UNSIGNED release artifacts.")
            }
        }
        debug {
            resValue "string", "app_name", "@string/app_name"
            debuggable true
        }
    }

    bundle {
        language {
            // Specifies that the app bundle should not support
            // configuration APKs for language resources. These
            // resources are instead packaged with each base and
            // dynamic feature APK.
            enableSplit = false
        }
    }

    // Configure only for each module that uses Java 8
    // language features (either in its source code or
    // through dependencies).
    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // For Kotlin projects
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/*']
        }
        resources {
            excludes += ['META-INF/*']
        }
    }


    namespace 'com.example.musicplayergo'
    lint {
        disable 'MissingTranslation', 'AllowBackup', 'GoogleAppIndexingWarning'
        baseline = file("lint-baseline.xml")
    }
}

kapt {
    correctErrorTypes = true
    useBuildCache = true
}

repositories {
    google()
    mavenCentral()
}

dependencies {
// Kotlin
    //https://github.com/JetBrains/kotlin/blob/master/ChangeLog.md
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    //https://github.com/Kotlin/kotlinx.coroutines/blob/master/CHANGES.md
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.2'

// Unit Testing (JVM tests)
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.11.1'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.test.ext:junit:1.1.5'

// Android Instrumentation Testing
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'

// Support
    //https://developer.android.com/jetpack/androidx/versions
    implementation "androidx.activity:activity-ktx:1.7.2"
    implementation 'androidx.browser:browser:1.5.0'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.window:window:1.1.0'
    implementation 'androidx.media:media:1.6.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    //https://github.com/material-components/material-components-android/releases
    implementation 'com.google.android.material:material:1.9.0'

// Third-party
    //https://mvnrepository.com/artifact/com.squareup.moshi/moshi-kotlin
    implementation 'com.squareup.moshi:moshi:1.15.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.15.0'

    //https://github.com/zhanghai/AndroidFastScroll/releases
    implementation 'me.zhanghai.android.fastscroll:library:1.2.0'

    //https://github.com/coil-kt/coil/releases
    implementation 'io.coil-kt:coil:2.4.0'

    //https://square.github.io/retrofit/
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    //https://square.github.io/okhttp/
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    // Gson for JSON parsing
    implementation 'com.google.code.gson:gson:2.10.1'
    // Firebase
    implementation(platform("com.google.firebase:firebase-bom:34.6.0"))
    implementation("com.google.firebase:firebase-analytics")
    implementation("com.google.firebase:firebase-firestore")
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'

// RecyclerViewActions
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'

// For ActivityScenarioRule
    debugImplementation 'androidx.fragment:fragment-testing:1.6.2'
    androidTestImplementation 'androidx.test:core:1.5.0'

// If using ViewPager2
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
}
